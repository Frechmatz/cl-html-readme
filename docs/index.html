<html><body><header><h1>cl-html-readme</h1>Generate HTML documentation for Common Lisp projects.
<p>The library follows a very simple approach: (1) define a HTML skeleton using the DSL of cl-html-readme and (2) provide the actual "content" via plain HTML strings.</p>
<p>The key features of cl-html-readme are:
    <ul>
	<li>Generate HTML heading and semantic elements.</li>
	<li>Generate a Table Of Contents. Provide full control of what should be listed in it.</li>
	<li>Easily embed source code files as well as verbatim text files via helper functions provided by the API.</li>
    </ul>
</p>
<p>Extraction of metadata such as documentation strings is not in the scope of this library.
    It is up to the documentation generation process of the specific project to implement
    metadata access as well as HTML rendering of this data.
</p><p>The source code is available <a href="https://github.com/Frechmatz/cl-html-readme">here</a>.</p></header><nav><h1>Table of contents</h1><ul><li><a href="#Installation">Installation</a></li><li><a href="#DSL">DSL</a><ul><li><a href="#Heading elements and Table of Contents">Heading elements and Table of Contents</a></li><li><a href="#Semantic elements">Semantic elements</a></li><li><a href="#Classes and Styles">Classes and Styles</a></li></ul></li><li><a href="#API">API</a></li><li><a href="#Documentation example">Documentation example</a></li></ul></nav><section><h1 id="Installation">Installation</h1><pre><code>
cd ~/quicklisp/local-projects
git clone https://github.com/Frechmatz/cl-html-readme.git
</code></pre><h1 id="DSL">DSL</h1>The DSL (Domain Specific Language) of cl-html-readme is used to declare an
HTML skeleton consisting of semantic elements, heading elements and a table of contents. Beside these predefined elements the actual "content" consists of plain strings
that can appear (almost) everywhere.
<p>The following HTML snippets have been formatted with <code>sgml-pretty-print</code>. The library
  itself does not support pretty-printing</p><h2 id="Heading elements and Table of Contents">Heading elements and Table of Contents</h2><h3>DSL</h3><p><pre><code>((heading (:name "Example"))
 (heading (:name "Table of contents")
          (toc))
 (heading (:name "Chapter 1" :toc t)) "Lorem ipsum"
 (heading (:name "Chapter 2" :toc t)) "Lorem ipsum")))</code></pre></p><h3>Generated HTML</h3><p><pre><code>&lt;h1&gt;Example
&lt;/h1&gt;
&lt;h1&gt;Table of contents
&lt;/h1&gt;
&lt;ul&gt;
  &lt;li&gt;
    &lt;a href="#Chapter 1"&gt;Chapter 1
    &lt;/a&gt;
  &lt;/li&gt;
  &lt;li&gt;
    &lt;a href="#Chapter 2"&gt;Chapter 2
    &lt;/a&gt;
  &lt;/li&gt;
&lt;/ul&gt;
&lt;h1 id="Chapter 1"&gt;Chapter 1
&lt;/h1&gt;Lorem ipsum
&lt;h1 id="Chapter 2"&gt;Chapter 2
&lt;/h1&gt;Lorem ipsum</code></pre></p><h2 id="Semantic elements">Semantic elements</h2><h3>DSL</h3><p><pre><code>((semantic (:name "header")
           (heading (:name "Example")))
 (semantic (:name "nav")
           (heading (:name "Table of contents")
                    (toc)))
 (semantic (:name "section")
           (heading (:name "Chapter 1" :toc t))
           "Lorem ipsum"
           (heading (:name "Chapter 2" :toc t))
           "Lorem ipsum")
 (semantic (:name "footer") "Lorem ipsum"))</code></pre></p><h3>Generated HTML</h3><p><pre><code>&lt;header&gt;
  &lt;h1&gt;Example
  &lt;/h1&gt;
&lt;/header&gt;
&lt;nav&gt;
  &lt;h1&gt;Table of contents
  &lt;/h1&gt;
  &lt;ul&gt;
    &lt;li&gt;
      &lt;a href="#Chapter 1"&gt;Chapter 1
      &lt;/a&gt;
    &lt;/li&gt;
    &lt;li&gt;
      &lt;a href="#Chapter 2"&gt;Chapter 2
      &lt;/a&gt;
    &lt;/li&gt;
  &lt;/ul&gt;
&lt;/nav&gt;
&lt;section&gt;
  &lt;h1 id="Chapter 1"&gt;Chapter 1
  &lt;/h1&gt;Lorem ipsum
  &lt;h1 id="Chapter 2"&gt;Chapter 2
  &lt;/h1&gt;Lorem ipsum
&lt;/section&gt;
&lt;footer&gt;Lorem ipsum
&lt;/footer&gt;</code></pre></p><h2 id="Classes and Styles">Classes and Styles</h2><h3>DSL</h3><p><pre><code>((semantic (:name "header" :class "semantic-class" :style "semantic-style" )
           (heading (:name "Example" :class "heading-class" :style "heading-style")))
 (semantic (:name "nav")
           (heading (:name "Table of contents")
                    (toc (:root-class "toc-root-class"
                          :root-style "toc-root-style"
                          :container-class "toc-container-class"
                          :container-style "toc-container-style"
                          :item-class "toc-item-class"
                          :item-style "toc-item-style"))))
 (semantic (:name "section")
           (heading (:name "Chapter 1" :toc t)
                    "Lorem ipsum"
                    (heading (:name "Chapter 1.1" :toc t)
                             "Lorem ipsum"))
           (heading (:name "Chapter 2" :toc t))
           "Lorem ipsum")
 (semantic (:name "footer") "Lorem ipsum"))</code></pre></p><h3>Generated HTML</h3><p><pre><code>&lt;header class="semantic-class" style="semantic-style"&gt;
  &lt;h1 class="heading-class" style="heading-style"&gt;Example
  &lt;/h1&gt;
&lt;/header&gt;
&lt;nav&gt;
  &lt;h1&gt;Table of contents
  &lt;/h1&gt;
  &lt;ul class="toc-root-class" style="toc-root-style"&gt;
    &lt;li class="toc-item-class" style="toc-item-style"&gt;
      &lt;a href="#Chapter 1"&gt;Chapter 1
      &lt;/a&gt;
      &lt;ul class="toc-container-class" style="toc-container-style"&gt;
        &lt;li class="toc-item-class" style="toc-item-style"&gt;
          &lt;a href="#Chapter 1.1"&gt;Chapter 1.1
          &lt;/a&gt;
        &lt;/li&gt;
      &lt;/ul&gt;
    &lt;/li&gt;
    &lt;li class="toc-item-class" style="toc-item-style"&gt;
      &lt;a href="#Chapter 2"&gt;Chapter 2
      &lt;/a&gt;
    &lt;/li&gt;
  &lt;/ul&gt;
&lt;/nav&gt;
&lt;section&gt;
  &lt;h1 id="Chapter 1"&gt;Chapter 1
  &lt;/h1&gt;Lorem ipsum
  &lt;h2 id="Chapter 1.1"&gt;Chapter 1.1
  &lt;/h2&gt;Lorem ipsum
  &lt;h1 id="Chapter 2"&gt;Chapter 2
  &lt;/h1&gt;Lorem ipsum
&lt;/section&gt;
&lt;footer&gt;Lorem ipsum
&lt;/footer&gt;</code></pre></p><h1 id="API">API</h1><b>*home-directory*</b><p>Home directory of the project for which documentation is to be generated.</p><b>*tab-width*</b><p>Width of a tab. Used, when tabs are to be replaced with space characters.</p><b>doc-to-html</b>&nbsp;(output-stream documentation)<p>Serializes a documentation object to HTML. The function has the following parameters:
   <ul>
       <li>output-stream A stream into which the resulting HTML is written.</li>
       <li>documentation A list following the syntax of the DSL.</li>
   </ul></p><b>make-path</b>&nbsp;(path)<p>Creates a path relative to *home-directory*. The function has the following parameters:
   <ul>
      <li>path The path, e.g. examples/example-1.lisp.</li>
   </ul></p><b>read-file</b>&nbsp;(path &key (replace-tabs nil) (escape nil))<p>Reads a text file and returns it as a string. The function has the following parameters:
   <ul>
      <li>path Path of the file relative to *home-directory*.</li>
      <li>:replace-tabs If t then tabs are replaced with spaces according to the *tab-width* variable.</li>
      <li>:escape If t then special characters are replaced with HTML character entities.</li>
   </ul></p><h1 id="Documentation example">Documentation example</h1>The following example shows how the readme of cl-html-readme is generated.
<ul>
    <li>Documentation strings are using HTML as markup.</li>
    <li>For extraction of documentation strings and function lambda-lists the <a href=\"https://github.com/eudoxia0/docparser\">docparser</a> library is used.</li>
</ul><h2>make-readme.lisp</h2><p><pre><code>(in-package :cl-html-readme-make-readme)

;;
;; Helper functions
;;

(defun create-index (system)
  (docparser:parse system))

(defun get-index-node (index package-name symbol-name)
  (aref (docparser:query
         index
         :package-name (string-upcase package-name)
         :symbol-name (string-upcase symbol-name))
        0))
  
(defun make-function-string (index package-name symbol-name)
  "Returns HTML representation of a function"
  (let* ((node (get-index-node index package-name symbol-name))
         (lambda-list (docparser:operator-lambda-list node)))
    (concatenate
     'string
     "&lt;b&gt;" (string-downcase symbol-name) "&lt;/b&gt;&amp;nbsp;"
     (string-downcase (format nil "~a" (if lambda-list lambda-list "()")))
     "&lt;p&gt;" (docparser:node-docstring node) "&lt;/p&gt;")))

(defun make-variable-string (index package-name symbol-name)
  "Returns HTML representation of a variable"
  (let ((node (get-index-node index package-name symbol-name)))
    (concatenate
     'string
     "&lt;b&gt;" (string-downcase symbol-name) "&lt;/b&gt;"
     "&lt;p&gt;" (docparser:node-docstring node) "&lt;/p&gt;")))
  
(defun make-code-string (path)
  "Returns HTML representation of a source code file"
  (concatenate
   'string
   "&lt;p&gt;&lt;pre&gt;&lt;code&gt;"
   (cl-html-readme:read-file path :replace-tabs t :escape t)
   "&lt;/code&gt;&lt;/pre&gt;&lt;/p&gt;"))

(defun now ()
  "Returns a string representing the current date and time."
  (multiple-value-bind (sec min hr day mon yr dow dst-p tz)
      (get-decoded-time)
    (declare (ignore dow dst-p tz))
    ;; 2018-09-19 21:28:16
    (let ((str (format nil "~4,'0d-~2,'0d-~2,'0d  ~2,'0d:~2,'0d:~2,'0d" yr mon day hr min sec)))
      str)))

;;
;; Readme
;;

(defun get-readme (index)
    `("&lt;html&gt;&lt;body&gt;"
      (semantic (:name "header")
                (heading (:name "cl-html-readme"))
                ,(cl-html-readme:read-file "make-readme/introduction.html")
                "&lt;p&gt;The source code is available &lt;a href=\"https://github.com/Frechmatz/cl-html-readme\"&gt;here&lt;/a&gt;.&lt;/p&gt;")
      (semantic (:name "nav")
                (heading (:name "Table of contents")
                         (toc)))
      (semantic (:name "section")
                (heading (:name "Installation" :toc t)
                         ,(cl-html-readme:read-file "make-readme/installation.html"))
                (heading (:name "DSL" :toc t)
                         ,(cl-html-readme:read-file "make-readme/dsl-introduction.html")
                         (heading (:name "Heading elements and Table of Contents" :toc t)
                                  (heading (:name "DSL")
                                           ,(make-code-string "make-readme/dsl-example-plain.dsl"))
                                  (heading (:name "Generated HTML")
                                           ,(make-code-string "make-readme/dsl-example-plain.html")))
                         (heading (:name "Semantic elements" :toc t)
                                  (heading (:name "DSL")
                                           ,(make-code-string "make-readme/dsl-example-semantic.dsl"))
                                  (heading (:name "Generated HTML")
                                           ,(make-code-string "make-readme/dsl-example-semantic.html")))
                         (heading (:name "Classes and Styles" :toc t)
                                  (heading (:name "DSL")
                                           ,(make-code-string "make-readme/dsl-example-styling.dsl"))
                                  (heading (:name "Generated HTML")
                                           ,(make-code-string "make-readme/dsl-example-styling.html"))))
                (heading (:name "API":toc t)
                         ,(make-variable-string index "cl-html-readme" "*home-directory*")
                         ,(make-variable-string index "cl-html-readme" "*tab-width*")
                         ,(make-function-string index "cl-html-readme" "doc-to-html")
                         ,(make-function-string index "cl-html-readme" "make-path")
                         ,(make-function-string index "cl-html-readme" "read-file"))
                (heading (:name "Documentation example" :toc t)
                         ,(cl-html-readme:read-file "make-readme/example-introduction.html")
                         (heading (:name "make-readme.lisp")
                                  ,(make-code-string "make-readme/make-readme.lisp"))))
      (semantic (:name "footer")
                "&lt;hr/&gt;&lt;p&gt;&lt;small&gt;Generated " ,(now) "&lt;/small&gt;&lt;/p&gt;")
      "&lt;/body&gt;&lt;/html&gt;"))

;;
;; Generate readme
;;

(defun make-readme ()
  (let ((index (create-index :cl-html-readme)))
    (let ((cl-html-readme:*home-directory* (asdf:system-source-directory :cl-html-readme-make-readme)))
      (with-open-file (fh (cl-html-readme:make-path "docs/index.html")
                          :direction :output
                          :if-exists :supersede
                          :if-does-not-exist :create
                          :external-format :utf-8)
        (cl-html-readme:doc-to-html fh (get-readme index)))))
    "DONE")

;;(make-readme)</code></pre></p></section><footer><hr/><p><small>Generated 2020-11-17  23:45:03</small></p></footer></body></html>